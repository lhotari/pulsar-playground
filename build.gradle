plugins {
    id 'application'
    id "io.freefair.lombok" version "8.7.1"
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'com.github.ben-manes.versions' version '0.51.0'
}

repositories {
    mavenLocal()
    mavenCentral()
}

application {
    mainClass = 'com.github.lhotari.pulsar.playground.TestScenarioCreateTopics'
}

def pulsarVersion = System.getenv('PULSAR_VERSION') ?: '3.3.1'

dependencies {
    testImplementation platform('org.junit:junit-bom:5.11.0')
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

    implementation "org.apache.pulsar:pulsar-client-original:${pulsarVersion}"
    implementation "org.apache.pulsar:pulsar-client-admin-original:${pulsarVersion}"

    implementation platform("org.apache.logging.log4j:log4j-bom:2.23.1")
    implementation "org.apache.logging.log4j:log4j-api"
    implementation "org.apache.logging.log4j:log4j-core"
    // use log4j-slf4j2-impl with Pulsar 3.3.0 and newer, for older versions use log4j-slf4j-impl
    implementation "org.apache.logging.log4j:log4j-slf4j2-impl"

    implementation 'io.github.resilience4j:resilience4j-retry:2.2.0'

    implementation 'com.beust:jcommander:1.82'

    implementation 'org.roaringbitmap:RoaringBitmap:1.2.1'
}

// Workaround for invalid metadata for Bookkeeper dependencies which contain <packaging>nar</packaging> in pom.xml
configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'org.apache.bookkeeper' &&
                details.requested.name in ['circe-checksum', 'cpu-affinity', 'native-io']) {
            details.artifactSelection { ArtifactSelectionDetails asDetails ->
                asDetails.selectArtifact('jar', null, null)
            }
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
