plugins {
    id 'application'
    id "io.freefair.lombok" version "8.1.0"
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

repositories {
    mavenLocal()
    mavenCentral()
}

application {
    mainClass = 'com.github.lhotari.pulsar.playground.TestScenarioCreateTopics'
}

def pulsarVersion = System.getenv('PULSAR_VERSION') ?: '3.2.2'

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

    implementation "org.apache.pulsar:pulsar-client-original:${pulsarVersion}"
    implementation "org.apache.pulsar:pulsar-client-admin-original:${pulsarVersion}"

    implementation "org.apache.logging.log4j:log4j-api:2.20.0"
    implementation "org.apache.logging.log4j:log4j-core:2.20.0"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:2.20.0"

    implementation 'io.github.resilience4j:resilience4j-retry:2.2.0'

    implementation 'com.beust:jcommander:1.82'

    implementation 'org.roaringbitmap:RoaringBitmap:1.0.1'
}

// Workaround for invalid metadata for Bookkeeper dependencies which contain <packaging>nar</packaging> in pom.xml
configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'org.apache.bookkeeper' &&
                details.requested.name in ['circe-checksum', 'cpu-affinity', 'native-io']) {
            details.artifactSelection { ArtifactSelectionDetails asDetails ->
                asDetails.selectArtifact('jar', null, null)
            }
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
